---
title: "qjson: Fetching all properties of a wikidata item in a single API call"
author: Santhosh Thottingal
type: post
date: 2025-04-15T05:00:00+05:30
url: /blog/2025/04/15/qjson/
categories:
  - Wikipedia
  - Wikidata

---


For those deeply involved with Wikidata, the richness of its interconnected data is both a blessing and a challenge when it comes to programmatic access. While the standard `wbgetentities` API endpoint is fundamental, retrieving the complete set of properties, including labels and values, for a given item often leads to a cascade of recursive API calls. For example, suppose we fetch all properties for Q42 using wbgetentities API - <https://www.wikidata.org/w/api.php?action=wbgetentities&ids=Q42>. In the response, if well lookup the "country of citizenship" (P27) for Q42 (Douglas Adams): the initial response only provides the target QID (Q145), necessitating further queries to resolve both P27 and Q145 into human-readable labels. This quickly becomes inefficient for comprehensive data extraction.

The need to retrieve *all* information visible on a Wikidata item's page – properties, statements, qualifiers, and their corresponding labels – is a common requirement. However, the inherent limitations of the standard API make this a cumbersome process.

The standard API's approach of returning only the QID for properties and values means that developers often find themselves in a loop of making multiple API calls to resolve these IDs into meaningful labels. This not only increases the complexity of the code but also leads to performance issues due to the high number of requests made to the API.

The solution lies in leveraging the expressive power of **SPARQL**. A carefully constructed SPARQL query can efficiently fetch all the required details in a single request. The following query demonstrates this capability:

```txt
SELECT
  ?property
  ?propertyLabel
  ?statementValue
  ?statementValueLabel
  ?statementValueImage
  ?qualifierProperty
  ?qualifierPropertyLabel
  ?qualifierValue
  ?qualifierValueLabel
  ?unitOfMeasure
  ?unitOfMeasureLabel
  ?statementRankLabel
WHERE {
  VALUES ?item {wd:%s}
  ?item ?propertyPredicate ?statement .
  ?statement ?statementPropertyPredicate ?statementValue .
  ?property wikibase:claim ?propertyPredicate .
  ?property wikibase:statementProperty ?statementPropertyPredicate .
  ?statement wikibase:rank ?statementRank .
  BIND(IF(?statementRank = wikibase:NormalRank, "Normal", IF(?statementRank = wikibase:PreferredRank, "Preferred", IF(?statementRank = wikibase:DeprecatedRank, "Deprecated", "Unknown"))) AS ?statementRankLabel)
  OPTIONAL { ?statementValue wdt:P18 ?statementValueImage . }
  OPTIONAL { ?statement ?qualifierPredicate ?qualifierValue . ?qualifierProperty wikibase:qualifier ?qualifierPredicate . }
  OPTIONAL { ?statement ?statementValueNodePredicate ?valueNode . ?valueNode wikibase:quantityUnit ?unitOfMeasure . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "%s, en" . ?property rdfs:label ?propertyLabel . ?statementValue rdfs:label ?statementValueLabel . ?qualifierProperty rdfs:label ?qualifierPropertyLabel . ?qualifierValue rdfs:label ?qualifierValueLabel . ?unitOfMeasure rdfs:label ?unitOfMeasureLabel . }
}
ORDER BY ?property ?statementValue ?qualifierProperty ?qualifierValue
```

Executing this query (replace `%s` with the desired QID, e.g., `Q42`) on the [Wikidata Query Service](https://query.wikidata.org/) yields the raw data.

Example for Q42: [query](https://query.wikidata.org/#%20%20%20%20SELECT%0A%20%20%20%20%20%20%20%20%3Fproperty%0A%20%20%20%20%20%20%20%20%3FpropertyLabel%0A%20%20%20%20%20%20%20%20%3FstatementValue%0A%20%20%20%20%20%20%20%20%3FstatementValueLabel%0A%20%20%20%20%20%20%20%20%3FstatementValueImage%0A%20%20%20%20%20%20%20%20%3FqualifierProperty%0A%20%20%20%20%20%20%20%20%3FqualifierPropertyLabel%0A%20%20%20%20%20%20%20%20%3FqualifierValue%0A%20%20%20%20%20%20%20%20%3FqualifierValueLabel%0A%20%20%20%20%20%20%20%20%3FunitOfMeasure%0A%20%20%20%20%20%20%20%20%3FunitOfMeasureLabel%0A%20%20%20%20%20%20%20%20%3FstatementRankLabel%0A%20%20%20%20WHERE%20%7B%0A%20%20%20%20%20%20%20%20VALUES%20%3Fitem%20%7Bwd%3AQ42%7D%0A%0A%20%20%20%20%20%20%20%20%3Fitem%20%3FpropertyPredicate%20%3Fstatement%20.%0A%20%20%20%20%20%20%20%20%3Fstatement%20%3FstatementPropertyPredicate%20%3FstatementValue%20.%0A%0A%20%20%20%20%20%20%20%20%3Fproperty%20wikibase%3Aclaim%20%3FpropertyPredicate%20.%0A%20%20%20%20%20%20%20%20%3Fproperty%20wikibase%3AstatementProperty%20%3FstatementPropertyPredicate%20.%0A%0A%20%20%20%20%20%20%20%20%3Fstatement%20wikibase%3Arank%20%3FstatementRank%20.%0A%20%20%20%20%20%20%20%20BIND%28%0A%20%20%20%20%20%20%20%20%20%20%20%20IF%28%3FstatementRank%20%3D%20wikibase%3ANormalRank%2C%20%22Normal%22%2C%0A%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20IF%28%3FstatementRank%20%3D%20wikibase%3APreferredRank%2C%20%22Preferred%22%2C%0A%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20IF%28%3FstatementRank%20%3D%20wikibase%3ADeprecatedRank%2C%20%22Deprecated%22%2C%20%22Unknown%22%29%0A%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%20%29%0A%20%20%20%20%20%20%20%20%20%20%20%20%29%20AS%20%3FstatementRankLabel%0A%20%20%20%20%20%20%20%20%29%0A%0A%20%20%20%20%20%20%20%20OPTIONAL%20%7B%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FstatementValue%20wdt%3AP18%20%3FstatementValueImage%20.%0A%20%20%20%20%20%20%20%20%7D%0A%0A%20%20%20%20%20%20%20%20OPTIONAL%20%7B%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Fstatement%20%3FqualifierPredicate%20%3FqualifierValue%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FqualifierProperty%20wikibase%3Aqualifier%20%3FqualifierPredicate%20.%0A%20%20%20%20%20%20%20%20%7D%0A%0A%20%20%20%20%20%20%20%20OPTIONAL%20%7B%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Fstatement%20%3FstatementValueNodePredicate%20%3FvalueNode%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FvalueNode%20wikibase%3AquantityUnit%20%3FunitOfMeasure%20.%0A%20%20%20%20%20%20%20%20%7D%0A%0A%20%20%20%20%20%20%20%20SERVICE%20wikibase%3Alabel%20%7B%0A%20%20%20%20%20%20%20%20%20%20%20%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%25s%2C%20en%22%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Fproperty%20rdfs%3Alabel%20%3FpropertyLabel%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FstatementValue%20rdfs%3Alabel%20%3FstatementValueLabel%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FqualifierProperty%20rdfs%3Alabel%20%3FqualifierPropertyLabel%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FqualifierValue%20rdfs%3Alabel%20%3FqualifierValueLabel%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3FunitOfMeasure%20rdfs%3Alabel%20%3FunitOfMeasureLabel%20.%0A%20%20%20%20%20%20%20%20%7D%0A%20%20%20%20%7D%0A%20%20%20%20ORDER%20BY%20%3Fproperty%20%3FstatementValue%20%3FqualifierProperty%20%3FqualifierValue%0A%20%20%20)

## qjson

While the SPARQL query provides the necessary data, the direct output often requires significant post-processing to achieve a usable, structured format. To address this, I've developed **[qjson](https://qjson.toolforge.org/)**, a Toolforge API designed for efficient and structured retrieval of all properties for a given Wikidata item.

qjson simplifies this process by handling the SPARQL query execution and structuring the response into a clean JSON format, complete with labels in the specified language.

**Usage Examples:**

* Fetching all data for Q405 (Moon) in English:
  <https://qjson.toolforge.org/Q405.json>
* Retrieving the same data in French:
  <https://qjson.toolforge.org/Q405.json?lang=fr>

To minimize unnecessary load on the Wikidata infrastructure, qjson implements response caching with a defined TTL. This ensures efficient data retrieval for frequently accessed items.

Source code: <https://gitlab.wikimedia.org/santhosh/qjson>. This tools is written in Go programming language.

## Disclaimer

I work at the Wikimedia Foundation. However, this project, exploration, and the opinions expressed are entirely my own and do not reflect my employer's views. This is not an official Wikimedia Foundation project.
